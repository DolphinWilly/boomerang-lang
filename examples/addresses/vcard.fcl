module Vcard =

(* According to http://www.jabber.org/jeps/jep-0054.html, a VCard should start with a version
   number, then is followed by mandatory FN and N. As every example seem not to have a version,
   we assume that the first element is a FN. *)

let hoist_optional_pcdata : lens = 
  acond {PCDATA = Value} Value
    (hoist "PCDATA")
    id

(* list of PCDATA at the top of the element *)
let flatten_several_depth1 (n:name) : lens =
      mapp {`n} (List.map (Xml.squash_flattened; hoist_optional_pcdata))

(* record of PCDATA at the top of the element *)
let flatten_several_depth2 (n:name) : lens =
      mapp {`n} (List.map (Xml.squash_flattened; map hoist_optional_pcdata))

let l : lens =
      (* we first bring the FN element, assumed to be the first, as key *)
      List.hd []; focus "vCard" {}; focus Xml.children {xmlns = {vcard-temp}};
      mapp {`List.HD} (Xml.squash_elt; hoist "FN"; hoist "PCDATA");
      pivot List.HD; map (hoist List.TL);
      (* we now squash the first element of the list, that is N, and flatten the rest *)
      map (wmap {`List.HD -> Xml.squash_elt, `List.TL -> Xml.flatten};
           hoist_nonunique List.HD {N}; rename List.TL "contents");
      (* we now hoist the PCDATA data under name N *)
      map (mapp {N} (map hoist_optional_pcdata));
      (* flattening birthdays, nicknames, title, url *)
      map (mapp {contents} (flatten_several_depth1 "BDAY";
                            flatten_several_depth1 "NICKNAME";
                            flatten_several_depth1 "TITLE";
                            flatten_several_depth1 "URL"));
      (* flattening addresses, email, tel *)
      map (mapp {contents} (flatten_several_depth2 "ADR";
                            flatten_several_depth2 "EMAIL";
                            flatten_several_depth2 "TEL"));
      (* ORG is special: one ORGNAME, several ORGUNITS *)
      map (mapp {contents} (mapp {ORG} (List.map (
        fork {ORGNAME}
          (Xml.squash_flattened; map hoist_optional_pcdata)
          (map (List.map hoist_optional_pcdata))))))
        


test l / (load "xml" "<vCard xmlns='vcard-temp'>
                      |  <FN>Alan Schmitt</FN>
                      |  <N>
                      |    <FAMILY>Schmitt</FAMILY>
                      |    <GIVEN>Alan</GIVEN>
                      |    <MIDDLE/>
                      |  </N>
                      |  <NICKNAME>brab</NICKNAME>
                      |  <URL>http://sardes.inrialpes.fr/~aschmitt/</URL>
                      |  <BDAY>1974-04-02</BDAY>
                      |  <ORG>
                      |    <ORGNAME>INRIA Rhône-Alpes</ORGNAME>
                      |    <ORGUNIT/>
                      |  </ORG>
                      |  <TITLE>Researcher</TITLE>
                      |  <TEL><VOICE/><WORK/><NUMBER>+33 (0)4 76 61 54 15</NUMBER></TEL>
                      |  <TEL><VOICE/><HOME/><NUMBER>+33 (0)8 71 79 74 00</NUMBER></TEL>
                      |  <ADR>
                      |    <HOME/>
                      |    <EXTADD/>
                      |    <STREET>4, rue de l'Isère</STREET>
                      |    <LOCALITY>Grenoble</LOCALITY>
                      |    <REGION></REGION>
                      |    <PCODE>380000</PCODE>
                      |    <CTRY>FRANCE</CTRY>
                      |  </ADR>
                      |  <EMAIL><INTERNET/><PREF/><USERID>alan.schmitt@polytechnique.org</USERID></EMAIL>
                      |</vCard>")
=
{"Alan Schmitt" =
  {N = {FAMILY = {Schmitt}, GIVEN = {Alan}, MIDDLE = {}},
   contents =
    {ADR =
      [{CTRY = {FRANCE},
        EXTADD = {},
        HOME = {},
        LOCALITY = {Grenoble},
        PCODE = {380000},
        REGION = {},
        STREET = {"4, rue de l'Isère"}}],
     BDAY = [{1974-04-02}],
     EMAIL =
      [{INTERNET = {},
        PREF = {},
        USERID = {"alan.schmitt@polytechnique.org"}}],
     NICKNAME = [{brab}],
     ORG = [{ORGNAME = {"INRIA Rhône-Alpes"}, ORGUNIT = [{}]}],
     TEL =
      [{NUMBER = {"+33 (0)4 76 61 54 15"},
        VOICE = {},
        WORK = {}},
       {HOME = {},
        NUMBER = {"+33 (0)8 71 79 74 00"},
        VOICE = {}}],
     TITLE = [{Researcher}],
     URL = [{"http://sardes.inrialpes.fr/~aschmitt/"}]}}}

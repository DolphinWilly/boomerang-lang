module Database = 

(* We start with a basic database that contains one table for email addresses
and one for phone numbers.  The email addresses include either the
relationship "prsn" for personal contacts or the type "prof" for professional
contacts.  There is an implicit FD "nm -> rel". *)

(* The phone numbers are given a sort, and it is assumed that the pair of
fields (nm, sort) is a key for the table. *)

let c : tree =
    { email_addr =
        [ { nm = {"Alice"}, email = {"alice@cis"},     rel = {"prof"} }
        , { nm = {"Bob"},   email = {"bob@bar.com"},   rel = {"prsn"} }
        , { nm = {"Bob"},   email = {"bob@foo.org"},   rel = {"prsn"} }
        , { nm = {"Carol"}, email = {"carol@foo.org"}, rel = {"prsn"} }
        , { nm = {"Fred"},  email = {"fred@cis"},      rel = {"prof"} }
        ]
    , phone_num =
        [ { nm = {"Alice"}, ph = {"123-456-7890"}, sort = {"cell"} }
        , { nm = {"Alice"}, ph = {"234-567-8901"}, sort = {"home"} }
        , { nm = {"Bob"},   ph = {"345-678-9012"}, sort = {"cell"} }
        , { nm = {"Dave"},  ph = {"456-789-0123"}, sort = {"home"} }
        , { nm = {"Ellen"}, ph = {"567-890-1234"}, sort = {"cell"} }
        ]
    }

(* We begin with a simple query on the "phone_num" table, in which we select
records representing cell phone numbers and project away the "sort" field. *)

let l1 : lens =
    Relational.select {nm=Any,ph=Any,sort={"cell"}}
        "phone_num" "cell_phone_num";
    Relational.project {"nm", "ph"} {"nm", "ph"} [{sort={"cell"}}]
        "cell_phone_num" "cell_phone_num"

let a1 : tree =
    { email_addr =
        [ { nm = {"Alice"}, email = {"alice@cis"},     rel = {"prof"} }
        , { nm = {"Bob"},   email = {"bob@bar.com"},   rel = {"prsn"} }
        , { nm = {"Bob"},   email = {"bob@foo.org"},   rel = {"prsn"} }
        , { nm = {"Carol"}, email = {"carol@foo.org"}, rel = {"prsn"} }
        , { nm = {"Fred"},  email = {"fred@cis"},      rel = {"prof"} }
        ]
    , cell_phone_num =
        [ { nm = {"Alice"}, ph = {"123-456-7890"} }
        , { nm = {"Bob"},   ph = {"345-678-9012"} }
        , { nm = {"Ellen"}, ph = {"567-890-1234"} }
        ]
    }

test l1 / c = a1

(* We delete Alice, add Carol, add Dave (who was present in the concrete
view), and modify Ellen. *)

let a1' : tree =
    { email_addr =
        [ { nm = {"Alice"}, email = {"alice@cis"},     rel = {"prof"} }
        , { nm = {"Bob"},   email = {"bob@bar.com"},   rel = {"prsn"} }
        , { nm = {"Bob"},   email = {"bob@foo.org"},   rel = {"prsn"} }
        , { nm = {"Carol"}, email = {"carol@foo.org"}, rel = {"prsn"} }
        , { nm = {"Fred"},  email = {"fred@cis"},      rel = {"prof"} }
        ]
    , cell_phone_num =
        [ { nm = {"Bob"},   ph = {"345-678-9012"} }
        , { nm = {"Carol"}, ph = {"678-901-2345"} }
        , { nm = {"Dave"},  ph = {"789-012-3456"} }
        , { nm = {"Ellen"}, ph = {"890-123-4567"} }
        ]
    }

let c1 : tree =
    { email_addr =
        [ { nm = {"Alice"}, email = {"alice@cis"},     rel = {"prof"} }
        , { nm = {"Bob"},   email = {"bob@bar.com"},   rel = {"prsn"} }
        , { nm = {"Bob"},   email = {"bob@foo.org"},   rel = {"prsn"} }
        , { nm = {"Carol"}, email = {"carol@foo.org"}, rel = {"prsn"} }
        , { nm = {"Fred"},  email = {"fred@cis"},      rel = {"prof"} }
        ]
    , phone_num =
        [ { nm = {"Alice"}, ph = {"234-567-8901"}, sort = {"home"} }
        , { nm = {"Bob"},   ph = {"345-678-9012"}, sort = {"cell"} }
        , { nm = {"Carol"}, ph = {"678-901-2345"}, sort = {"cell"} }
        , { nm = {"Dave"},  ph = {"456-789-0123"}, sort = {"home"} }
        , { nm = {"Dave"},  ph = {"789-012-3456"}, sort = {"cell"} }
        , { nm = {"Ellen"}, ph = {"890-123-4567"}, sort = {"cell"} }
        ]
    }

test l1 \ a1' c = c1

(* We next try a less trivial projection. *)

let l2 : lens =
    Relational.project {"nm", "rel"} {"nm"} [{email={""}}]
        "email_addr" "relation"

let a2 : tree =
    { relation =
        [ { nm = {"Alice"}, rel = {"prof"} }
        , { nm = {"Bob"},   rel = {"prsn"} }
        , { nm = {"Carol"}, rel = {"prsn"} }
        , { nm = {"Fred"},  rel = {"prof"} }
        ]
    , phone_num =
        [ { nm = {"Alice"}, ph = {"234-567-8901"}, sort = {"home"} }
        , { nm = {"Bob"},   ph = {"345-678-9012"}, sort = {"cell"} }
        , { nm = {"Carol"}, ph = {"678-901-2345"}, sort = {"cell"} }
        , { nm = {"Dave"},  ph = {"456-789-0123"}, sort = {"home"} }
        , { nm = {"Dave"},  ph = {"789-012-3456"}, sort = {"cell"} }
        , { nm = {"Ellen"}, ph = {"890-123-4567"}, sort = {"cell"} }
        ]
    }

test l2 / c1 = a2

(* We modify the relationship of Bob from "prsn" to "prof"; we delete Carol;
and we add Dave. *)

let a2' : tree =
    { relation =
        [ { nm = {"Alice"}, rel = {"prof"} }
        , { nm = {"Bob"},   rel = {"prof"} }
        , { nm = {"Dave"},  rel = {"prsn"} }
        , { nm = {"Fred"},  rel = {"prof"} }
        ]
    , phone_num =
        [ { nm = {"Alice"}, ph = {"234-567-8901"}, sort = {"home"} }
        , { nm = {"Bob"},   ph = {"345-678-9012"}, sort = {"cell"} }
        , { nm = {"Carol"}, ph = {"678-901-2345"}, sort = {"cell"} }
        , { nm = {"Dave"},  ph = {"456-789-0123"}, sort = {"home"} }
        , { nm = {"Dave"},  ph = {"789-012-3456"}, sort = {"cell"} }
        , { nm = {"Ellen"}, ph = {"890-123-4567"}, sort = {"cell"} }
        ]
    }

let c2 : tree =
    { email_addr =
        [ { nm = {"Dave"},  email = {""},              rel = {"prsn"} }
        , { nm = {"Alice"}, email = {"alice@cis"},     rel = {"prof"} }
        , { nm = {"Bob"},   email = {"bob@bar.com"},   rel = {"prof"} }
        , { nm = {"Bob"},   email = {"bob@foo.org"},   rel = {"prof"} }
        , { nm = {"Fred"},  email = {"fred@cis"},      rel = {"prof"} }
        ]
    , phone_num =
        [ { nm = {"Alice"}, ph = {"234-567-8901"}, sort = {"home"} }
        , { nm = {"Bob"},   ph = {"345-678-9012"}, sort = {"cell"} }
        , { nm = {"Carol"}, ph = {"678-901-2345"}, sort = {"cell"} }
        , { nm = {"Dave"},  ph = {"456-789-0123"}, sort = {"home"} }
        , { nm = {"Dave"},  ph = {"789-012-3456"}, sort = {"cell"} }
        , { nm = {"Ellen"}, ph = {"890-123-4567"}, sort = {"cell"} }
        ]
    }

test l2 \ a2' c1 = c2

(* We may also perform a natural join on the tables after the previous
projection. *)

let l3 : lens =
    l2;
    Relational.ijoin1 "phone_num" "relation" "phone_num"

let a3 : tree =
    { phone_num =
        [ { nm = {"Alice"}, ph = {"234-567-8901"}, sort = {"home"}, rel = {"prof"} }
        , { nm = {"Bob"},   ph = {"345-678-9012"}, sort = {"cell"}, rel = {"prof"} }
        , { nm = {"Dave"},  ph = {"456-789-0123"}, sort = {"home"}, rel = {"prsn"} }
        , { nm = {"Dave"},  ph = {"789-012-3456"}, sort = {"cell"}, rel = {"prsn"} }
        ]
    }

test l3 / c2 = a3

(* We add an entry for Carol, which will overwrite her previous entry, and one
for Dave, which will overwrite part of his entry (but not his email address).
We will delete Alice, which will delete her only from the "phone_num" table.
We will also add a brand new entry, which will result in additions to both
tables in the concrete view. *)

let a3' : tree =
    { phone_num =
        [ { nm = {"Bob"},   ph = {"345-678-9012"}, sort = {"cell"}, rel = {"prof"} }
        , { nm = {"Carol"}, ph = {"901-234-5678"}, sort = {"home"}, rel = {"prsn"} }
        , { nm = {"Dave"},  ph = {"456-789-0123"}, sort = {"home"}, rel = {"prsn"} }
        , { nm = {"Dave"},  ph = {"789-012-3456"}, sort = {"cell"}, rel = {"prsn"} }
        , { nm = {"Fred"},  ph = {"111-111-1111"}, sort = {"cell"}, rel = {"prsn"} }
        , { nm = {"Fred"},  ph = {"222-222-2222"}, sort = {"home"}, rel = {"prsn"} }
        , { nm = {"Greta"}, ph = {"333-333-3333"}, sort = {"cell"}, rel = {"prof"} }
        ]
    }

let c3 : tree =
    { email_addr =
        [ { nm = {"Carol"}, email = {""},              rel = {"prsn"} }
        , { nm = {"Dave"},  email = {""},              rel = {"prsn"} }
        , { nm = {"Greta"}, email = {""},              rel = {"prof"} }
        , { nm = {"Alice"}, email = {"alice@cis"},     rel = {"prof"} }
        , { nm = {"Bob"},   email = {"bob@bar.com"},   rel = {"prof"} }
        , { nm = {"Bob"},   email = {"bob@foo.org"},   rel = {"prof"} }
        , { nm = {"Fred"},  email = {"fred@cis"},      rel = {"prsn"} }
        ]
    , phone_num =
        [ { nm = {"Bob"},   ph = {"345-678-9012"}, sort = {"cell"} }
        , { nm = {"Carol"}, ph = {"901-234-5678"}, sort = {"home"} }
        , { nm = {"Dave"},  ph = {"456-789-0123"}, sort = {"home"} }
        , { nm = {"Dave"},  ph = {"789-012-3456"}, sort = {"cell"} }
        , { nm = {"Ellen"}, ph = {"890-123-4567"}, sort = {"cell"} }
        , { nm = {"Fred"},  ph = {"111-111-1111"}, sort = {"cell"} }
        , { nm = {"Fred"},  ph = {"222-222-2222"}, sort = {"home"} }
        , { nm = {"Greta"}, ph = {"333-333-3333"}, sort = {"cell"} }
        ]
    }

test l3 \ a3' c2 = c3


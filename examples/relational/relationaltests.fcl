module Relationaltests =

let orig : view =
  {{{
    Albums (Album,           Quantity) = {
           (Disintegration,  6       )
           (Show,            3       )
           (Galore,          1       )
           (Paris,           4       )
           (Wish,            5       )   }

    Tracks (Track,    Date, Rating, Album) = {
           (Lulluby,  1989, 3,      Galore)
           (Lulluby,  1989, 3,      Show) 
           (Lovesong, 1989, 5,      Galore)
           (Lovesong, 1989, 5,      Paris)
           (Trust,    1992, 4,      Wish)
    }
  }}} 

let origschema : schema =
  {{
     Albums(Album, Quantity) with {Album -> Quantity}, 
     Tracks(Track, Date, Rating, Album) with {Track -> Date, Track -> Rating}
  }}

let targetschema : schema =
  {{
     Tracks3(Track, Rating, Album, Quantity)
       with {Album -> Quantity, Track -> Rating}
       where (Quantity <> "0" /\ (Quantity <> "1" /\ Quantity <> "2"))
  }}

let l1 : lens = 
  Relational.join_dl "Tracks" with {Track -> Date, Track -> Rating}
    "Albums" with {Album -> Quantity}
      "Tracks1" 

let view1 : view =
  l1 / orig

let l2 : lens =
  Relational.drop "Tracks1" "Tracks2" "Date" {Track} "unknown"

let l3 : lens =
  Relational.select "Tracks2" with {Album -> Quantity, Track -> Rating} "Tracks3"
    where (Quantity <> "0" /\ (Quantity <> "1" /\ Quantity <> "2"))

let l : lens = (l1;l2;l3)

let _ : lens = check l : origschema <<-> targetschema

let view3 : view =
  l / orig

let newtarget : view =
  {{{ Tracks3(Track, Rating, Album, Quantity) =
        {(Lovesong, 5, Disintegration, 7),
         (Lulluby, 4, Show, 3)} }}}

(* This is the example from the dblenses paper: *)
test l \ newtarget orig = 
  {{{ Albums(Album, Quantity) =
           {(Disintegration, 7),
            (Galore, 1),
            (Paris, 4),
            (Show, 3),
            (Wish, 5)},
      Tracks(Track, Date, Rating, Album) =
           {(Lovesong, 1989, 5, Disintegration),
            (Lovesong, 1989, 5, Galore),
            (Lulluby, 1989, 4, Galore),
            (Lulluby, 1989, 4, Show)} }}}


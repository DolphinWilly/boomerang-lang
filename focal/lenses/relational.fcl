#{@}

\section{Module {\tt Relational}}
\label{sec:relational}

##module Relational =

##let rename : (name -> name -> name -> name -> lens) = Native.Relational.rename

#{*}
let _ : lens =
  check (rename "R" "S" "A" "D") :
  {{ R(A, B, C) with {(A, C) -> B} where A <> B }}
  <<~>
  {{ S(B, C, D) with {(C, D) -> B} where B = D -> FALSE }}

test rename "R" "S" "A" "C" /
  {{{ R(A, B) = {(1, 2)} }}}
= {{{ S(B, C) = {(2, 1)} }}}
test rename "R" "S" "A" "C" \
  {{{ S(B, C) = {(3, 4),
                 (2, 1)} }}}
  {{{ R(A, B) = {(1, 2)} }}}
= {{{ R(A, B) = {(1, 2),
                 (4, 3)} }}}
#{@}

##let select : (name -> fds -> name -> pred -> lens) = Native.Relational.select

#{*}
let _ : lens =
  check (select "R" with {A -> B} "S" where C = "1") :
  {{ R(A, B, C) with {A -> B} }}
  <<->
  {{ S(A, B, C) where C = "1" with {A -> B} }}

test select "R" with {} "S" where (A = "1" \/ B = C) /
  {{{ R(A, B, C) = {(1, 1, 1),
                    (2, 2, 2),
                    (1, 2, 3),
                    (3, 2, 1)} }}}
= {{{ S(A, B, C) = {(1, 1, 1),
                    (2, 2, 2),
                    (1, 2, 3)} }}}

test select "R" with {A -> B} "S" where C = "1" /
  {{{ R(A, B, C) = {(1, 2, 1),
                    (1, 2, 3),
                    (3, 2, 1),
                    (3, 2, 3)} }}}
= {{{ S(A, B, C) = {(1, 2, 1),
                    (3, 2, 1)} }}}

test select "R" with {A -> B} "S" where C = "1" \
  {{{ S(A, B, C) = {(1, 3, 1),
                    (4, 4, 1)} }}}
  {{{ R(A, B, C) = {(1, 2, 1),
                    (1, 2, 3),
                    (3, 2, 1),
                    (3, 2, 3)} }}}
= {{{ R(A, B, C) = {(1, 3, 1),
                    (1, 3, 3),
                    (4, 4, 1),
                    (3, 2, 3)} }}}

(* "Surprising" example from PODS paper. *)
test select "R" with {A -> B} "S" where B = "2" \
  {{{ S(A, B, C) = {(1, 2, 2)} }}}
  {{{ R(A, B, C) = {(1, 1, 1)} }}}
= {{{ R(A, B, C) = {(1, 2, 2)} }}}
#{@}

##let drop : (name -> name -> name -> view -> name -> lens) = Native.Relational.drop

#{*}
let _ : lens =
  check (drop "R" "S" "B" {B} "0") :
  {{ R(A, B) }}
  <<->
  {{ S(A) }}

let _ : lens =
  check (drop "R" "S" "B" {B} "0") :
  {{ R(A, B, C) where (A = C /\ B = "0") /\ (C = "3" /\ A <> B) }}
  <<->
  {{ S(A, C) where A = "3" /\ A = C }}

let _ : lens =
  check (drop "R" "S" "D" {B, C} "0") :
  {{ R(A, B, C, D) with {A -> C, (B, C) -> D} }}
  <<->
  {{ S(A, B, C) with {A -> C} }}

test drop "R" "S" "C" {C} "0" /
  {{{ R(A, B, C) = {(1, 1, 1)} }}}
= {{{ S(A, B)    = {(1, 1)}    }}}

test drop "R" "S" "C" {C} "0" \
  {{{ S(A, B)    = {(1, 1)}    }}}
  {{{ R(A, B, C) = {(1, 1, 1),
                    (1, 1, 2),
                    (1, 2, 3)} }}}
= {{{ R(A, B, C) = {(1, 1, 1),
                    (1, 1, 2)} }}}

test drop "R" "S" "C" {B} "0" \
  {{{ S(A, B)    = {(1, 1),
                    (2, 1),
                    (2, 2)}    }}}
  {{{ R(A, B, C) = {(1, 1, 3)} }}}
= {{{ R(A, B, C) = {(1, 1, 3),
                    (2, 1, 3),
                    (2, 2, 0)} }}}

test drop "R" "S" "C" {B} "0" \
  {{{ S(A, B)    = {(1, 1),
                    (2, 1),
                    (2, 2)}    }}}
  {{{ R(A, B, C) = {(3, 1, 3)} }}}
= {{{ R(A, B, C) = {(1, 1, 3),
                    (2, 1, 3),
                    (2, 2, 0)} }}}
#{@}

##let join_dl : (name -> fds -> name -> fds -> name -> lens) = Native.Relational.join_dl

#{*}
let _ : lens =
  check (join_dl "R" with {A -> (B,C)} "S" with {B -> (C,D)} "R") :
  {{ R(A, B, C) with {A -> (B,C)},
     S(B, C, D) with {B -> (C,D)} }}
  <<->
  {{ R(A, B, C, D) with {A -> (B,C), B -> (C,D)} }}

test join_dl "R" with {} "S" with {B -> C} "T" /
  {{{ R(A, B) = {(1, 1),
                 (2, 1),
                 (3, 3)}
      S(B, C) = {(1, 1),
                 (2, 4),
                 (3, 9),
                 (4, 16)} }}}
= {{{ T(A, B, C) = {(1, 1, 1),
                    (2, 1, 1),
                    (3, 3, 9)} }}}

test join_dl "R" with {} "S" with {B -> C} "T" \
  {{{ T(A, B, C) = {(1, 1, 2),
                    (3, 4, 9)} }}}
  {{{ R(A, B) = {(1, 1),
                 (2, 1),
                 (3, 3)}
      S(B, C) = {(1, 1),
                 (2, 4),
                 (3, 9),
                 (4, 16)} }}}
= {{{ R(A, B) = {(1, 1),
                 (3, 4)}
      S(B, C) = {(1, 2),
                 (2, 4),
                 (3, 9),
                 (4, 9)} }}}

#{@}


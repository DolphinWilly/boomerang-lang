// Example taken from the Toupie User's Manual
//
//   DONALD     526485
// + GERALD   + 197485
// --------   --------
// = ROBERT   = 723970
//
// D = 5, G = 1, R = 7, O = 2, N = 6, A = 4, L = 8, E = 9, B = 3, T = 0
//

E. r1 (E. r2 (E. r3 (E. r4 (E. r5 (E. r6(

//
// constraints on carries
//
r1 in [0,1] && r2 in [0,1] && r3 in [0,1] && r4 in [0,1] && r5 in [0,1] && r6 in [0,1] &&

//
// constraints on letters
//
d in [1,9] && g in [1,9] && r in [0,9] && // the first letter can't be 0
o in [0,9] && n in [0,9] && a in [0,9] && l in [0,9] && e in [0,9] && b in [0,9] && t in [0,9] && 

//
// description of the addition
//
   2*d = 10*r1+t &&
r1+2*l = 10*r2+r &&
r2+2*a = 10*r3+e &&
r3+n+r = 10*r4+b &&
r4+o+e = 10*r5+o &&
r5+d+g = 10*r6+r &&
r6 = 0 &&

//
// all letters have different values
//
d != o && d != n && d != a && d != l && d != g && d != e && d != r && d != b && d != t &&
o != n && o != a && o != l && o != g && o != e && o != r && o != b && o != t &&
n != a && n != l && n != g && n != e && n != r && n != b && n != t &&
a != l && a != g && a != e && a != r && a != b && a != t &&
l != g && l != e && l != r && l != b && l != t &&
g != e && g != r && g != b && g != t &&
e != r && e != b && e != t &&
r != b && r != t &&
b != t 

))))))

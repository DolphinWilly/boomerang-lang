let all = [^]*
let Title = [0-9a-zA-z ]+
let Content = all - (all.("\\subsection"|"\\section"|"\\end{document}").all)
let H1 = all - (all."\\begin{document}".all)
let H = H1."\\begin{document}".Content
let E = "\\end{document}".all



let chunk_subsection =
default
  (del "\\subsection{". ins "**".
  key Title.
  del "}".
  del Content.
  const "" "\n" "")
"\\subsection{NO NAME}\n***TO BE WRITTEN***\n\n" 

let extract_subsections = <chunk_subsection>*


let chunk_section =
default
  (del "\\section{". ins "*".
  key Title.
  del "}".
  del Content.
  const "" "\n" "".
  extract_subsections)
"\\section{NO NAME}\n***TO BE WRITTEN***\n\n" 


let extract_titles =  del H.<chunk_section>*. del E







(***** begining of the examples *******)

(*** test the suppression ***)

test extract_titles get

<<<\\documentclass[a4paper,12pt]{article}

\\begin{document}

\\tableofcontents
Some introductions
\\section{1st section}
blah blah
\\subsection{Sub of fst}
blahblahblah
\\section{Snd one}
\\subsection{Subsection of snd section}
the text of the subsection of snd section
more text

\\end{document}

ignored text

\\section{ignored title}

>>>
=
<<<*1st section
**Sub of fst
*Snd one
**Subsection of snd section
>>>



(*** test of creation ***)


test extract_titles put
  <<<
    *Intro
    *1st section
    **New subsection
    *New section
    
  >>>
into
  <<<
    \begin{document}
    \section{1st section}
    text fst section
    \end{document}
  >>> 
= 
  <<<
    \begin{document}
    \section{Intro}
    ***TO BE WRITTEN***

    \section{1st section}
    text fst section
    \subsection{New subsection}
    ***TO BE WRITTEN***

    \section{New section}
    ***TO BE WRITTEN***

    \end{document}
  >>>



(*** test of reordering inside a section ***)

test extract_titles put
<<<*Fst section
**Snd subsection
**Fst subsection
>>>

into

  <<<
    \begin{document}
    \section{Fst section}
    text fst section
    \subsection{Fst subsection}
    text fst subsection
    \subsection{Snd subsection}
    text snd subsection
    \end{document}
  >>> 
=
  <<<
    \begin{document}
    \section{Fst section}
    text fst section
    \subsection{Snd subsection}
    text snd subsection
    \subsection{Fst subsection}
    text fst subsection
    \end{document}
  >>> 



(*** everything together ! ***)
test extract_titles put
  <<<
    *Snd section
    **Fst subsection
    **Snd subsection
    *Fst section
    **Snd subsection
    **Fst subsection
    
  >>>

into
  <<<
    \begin{document}
    \section{Fst section}
    text fst section
    \subsection{Fst subsection}
    text 1.1
    \subsection{Snd subsection}
    text 1.2
    \section{Snd section}
    text snd section
    \subsection{Fst subsection}
    text 2.1
    \subsection{Snd subsection}
    text 2.2
    \end{document}
  >>> 
=
  <<<
    \begin{document}
    \section{Snd section}
    text snd section
    \subsection{Fst subsection}
    text 2.1
    \subsection{Snd subsection}
    text 2.2
    \section{Fst section}
    text fst section
    \subsection{Snd subsection}
    text 1.2
    \subsection{Fst subsection}
    text 1.1
    \end{document}
>>> 


(*** impossibility of moving from one section to another ***)

test extract_titles put
  <<<
    *Fst section
    *Snd section
    **Fst subsection
    
  >>>

into

  <<<
    \begin{document}
    \section{Fst section}
    text fst section
    \subsection{Fst subsection}
    text 1.1
    \section{Snd section}
    text snd section
    \end{document}
  >>> 
= 
  <<<
    \begin{document}
    \section{Fst section}
    text fst section
    \section{Snd section}
    text snd section
    \subsection{Fst subsection}
    ***TO BE WRITTEN***
    
    \end{document}
  >>>

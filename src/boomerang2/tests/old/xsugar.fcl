
let ws = "[ \n\t]*"
let opt_ws_e = / ws : "" /
let opt_ws_nl =  / ws : "\n" /
let opt_ws_2 =  / ws : "\n  " /
let opt_ws_4 = / ws : "\n    " /

(*  ----------- student data ----------- *)
let NAME = "[a-zA-Z]([ a-zA-Z]*[a-zA-z])*"
let EMAIL = "[a-zA-Z0-9@\\._]*"
let ID = "[0-9]{8}"

match t with 
  let id = opt_ws_2 "<student sid=\"" ~ "" ID "\">" ~ "\n" in 
  let name = opt_ws_4 "<name>" ~ "" NAME "</name>" ~ " " in 
  let email = opt_ws_4 "<email>" ~ "(" EMAIL "</email>" ~ ") " in 
    swap <id> <name email> opt_ws_2 "</student>"~"" opt_ws_e
default "
        |  <student sid=\"00000000\">
        |    <name>UNKNOWN</name>
        |    <email>UNKNOWN</email>
        |  </student>"

let file = "<students>" ~ "" [t]* opt_ws_nl "</students>" ~ ""

test file get put
  "<students>
  |</students>" 
= 
  ""

test file get put
  "<students>
  |  <student sid=\"19701234\">
  |    <name>John Doe</name>
  |    <email>john_doe@notmail.org</email>
  |  </student>
  |  <student sid=\"19785678\">
  |    <name>Jane Dow</name>
  |    <email>dow@bmail.org</email>
  |  </student>
  |</students>"
=   
  "John Doe (john_doe@notmail.org) 19701234
  |Jane Dow (dow@bmail.org) 19785678
  |"

(* now let's have an ASCII view *)
match t with 
  let id = opt_ws_2 "<student sid=\"" ~ "" key <ID> "\">" ~ "\n" in 
  let name = opt_ws_4 "<name>" ~ "" NAME "</name>" ~ ":" in 
  let del_email = opt_ws_4 "<email>" ~ "" / EMAIL : "UNKNOWN"/ "</email>" ~ "" in 
    swap <id> <name del_email> opt_ws_2 "</student>"~""
default "
        |  <student sid=\"00000000\">
        |    <name>UNKNOWN</name>
        |    <email>UNKNOWN</email>
        |  </student>"

let file_view = "<students>" ~ "" [t]* opt_ws_nl "</students>" ~ ""

test file_view get 
  "<students>
  |  <student sid=\"19701234\">
  |    <name>John Doe</name>
  |    <email>john_doe@notmail.org</email>
  |  </student>
  |  <student sid=\"19785678\">
  |    <name>Jane Dow</name>
  |    <email>dow@bmail.org</email>
  |  </student>
  |</students>"
= 
  "John Doe:19701234
  |Jane Dow:19785678
  |"

(* notice that whitespace is restored too *)
test file_view put
  "Janie Dow:19785678
  |Johnny Doe:19701234
  |"

  "<students>
  |  <student sid=\"19701234\">
  |  <name>John Doe</name>
  |  <email>john_doe@notmail.org</email>
  |  </student>
  |  <student sid=\"19785678\">
  |    <name>Jane Dow</name>
  |    <email>dow@bmail.org</email>
  |  </student>
  |</students>"
= 
  "<students>
  |  <student sid=\"19785678\">
  |    <name>Janie Dow</name>
  |    <email>dow@bmail.org</email>
  |  </student>
  |  <student sid=\"19701234\">
  |  <name>Johnny Doe</name>
  |  <email>john_doe@notmail.org</email>
  |  </student>
  |</students>"




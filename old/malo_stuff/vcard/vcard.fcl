let filter p d = fork p id (const {} d)
let focus n d = filter {n} d;hoist n
let hd d = focus *h {*t=d}
let tl d = focus *t {*h=d}
let prune n d = fork {n} (const {} {n=d}) id
let rename m n= xfork {m} {n} (hoist m;plunge n) id
let grename m n = cond (hasChild m) (hasChild n) (rename m n) id
let hoist_hd p = hoist_nonunique *h p;hoist_nonunique *t (neg p)

let map_head l = fork {*h} (map l) id
let map_tail l = fork {*t} (map l) id
let mapp p l = fork p (map l) id
let idfork pc pa l = xfork pc pa (cond isEmpty isEmpty id l) id

let partition p =
 let rec split =
  cond (eqDom {*}) (inter (child t isEmptyList) (child f isEmptyList))
   (plunge t;add f [])
   (mapk <*t -> split>;
    hoist_nonunique *t {t f};
    gcond (child *h (minus all p)) (child f (minus all isEmptyList)) (child t (minus all isEmptyList))
     (rename f *t;xfork {* *h *t} {f} (plunge f) id)
     (rename t *t;xfork {* *h *t} {t} (plunge t) id)
     (id)
     (id)
    ;probe end)
 in split

let value = hd [];
 xfork {PARAM} {} (const {} {}) (hoist DATA;hd [])
let list_value = hd [];
 xfork {PARAM} {} (const {} {}) (hoist DATA)

let name =
 grename N CompleteName;grename FN FullName;
 grename NICKNAME NickName;grename TITLE Title;
 mapk < CompleteName -> list_value FullName -> value 
	NickName -> value Title -> value >

let tel =
 let aux = 
  ccond (child PARAM (inter (hasChild CELL) (hasChild WORK)))
  id (
  ccond (child PARAM (hasChild CELL))
  (focus DATA {PARAM=CELL};hd [];plunge Cell) (
  ccond (child PARAM (hasChild FAX))
  (focus DATA {PARAM=FAX};hd [];plunge Fax) (
  id)
  )
  )
  in
 map_list aux

let contact_lens =
 hoist VCARD;
 flatten_list;
 filter {NICKNAME N FN ROLE EMAIL TITLE TEL NOTE} {};
 idfork {NICKNAME N FN TITLE} {Name}
  (name;plunge Name);
 idfork {ROLE} {Role}
  (rename ROLE Role;map value);
 idfork {EMAIL} {Email}
  (rename EMAIL Email;map value);
 idfork {TEL} {Telephone}
  (rename TEL Telephone;map tel);
 plunge Contact

let vcard_to_address_book =
  map_list contact_lens;
  plunge Address_book

do vcard_to_address_book
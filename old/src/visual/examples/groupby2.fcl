(* groupby2 lens *)

(* fork/map helpers *)
let idFork p l = fork p l id
let idXFork pc pa l = xfork pc pa l id

(* note: mapName takes a predicate, but it *must* contain 
   a single name when we have variables in views, we should 
   change it so that it takes a name instead 
*)
let mapName p l = idFork p (map l)

(* list helpers *)
let addAtomic = add * {}
let addEmptyTail = add *t {}; mapName {*t} (addAtomic)
let listifyHd = addAtomic; addEmptyTail

let rec groupby2 =
  acond isEmptyList isEmptyList
        (id)
        (rename *h x1;
         rename * a1;
         hoist_nonunique *t {*t *h *};
         acond (eqDom {x1 a1 *}) singListSingList
                (idXFork {*} {*t} (plunge *t);
                 rename x1 *h;
                 rename a1 *;
                 plunge *h;
		 listifyHd
                )
                (
                 mapName {*h} (plunge *h);
                 hoist_nonunique *h {*h};
                 idXFork {* *h} {x2} (plunge x2);
                 mapName {x2} (addEmptyTail);
                 xfork { x1 x2 a1} {*h *}
                       (rename x1 *h;
                        rename x2 *t;
			rename a1 *;
                         plunge *h;
                         addAtomic
                       )
                       (mapName {*t} groupby2)
                )

        )
do groupby2
